/**
 * TantalumLibrary/build.gradle
 *
 * Builds the TantalumLibrary-project. The project contains an extra config, called 'codeCoverageJar', which
 * packages and exposes the cobertura-instrumented classes as a jar file.
 */

sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

// Run static checks on the library code
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'pmd'

// Don't fail the build for static analysis warnings
project.findbugs.ignoreFailures = true
project.pmd.ignoreFailures = true

configurations {
    codeCoverageJar
}

task jarInstrumented(type: Jar) {
    baseName = project.name + '-instrumented'
    dependsOn coberturaInstrumentMain
    from sourceSets.main.output
    from sourceSets.main.cobertura.output
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    codeCoverageJar jarInstrumented
    archives sourcesJar
    archives javadocJar
}

task compileJava(overwrite: true, dependsOn: wtkbuild) << {
}
