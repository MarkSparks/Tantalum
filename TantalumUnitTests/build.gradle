sourceSets {
    main {
        java {
            srcDir 'src'
        }
    }
}

dependencies {
    compile project(':TantalumLibrary')
    compile files('../lib/jmunit4cldc11-1.2.1.jar')

    testCompile "org.microemu:microemulator:2.0.4"
    testCompile "org.microemu:microemu-midp:2.0.4"
    testCompile "org.microemu:microemu-cldc:2.0.4"
    testCompile "org.microemu:microemu-jsr-75:2.0.4"
    testCompile("org.microemu:microemu-jsr-135:2.0.4") {
        exclude module: 'microemu-midp'
        exclude module: 'microemu-cldc'
    }

}

/**
 * Package the MIDlet package containing the unit tests to run on a device/emulator
 */
task wtkpackage << {
    project.ext.jadFile = target + '/wtk/' + project.name + '.jad'
    project.ext.jarFile = target + '/wtk/' + project.name + '.jar'

    ant.copy(file: projectDir.toString() + "/Application Descriptor",
        tofile: project.ext.jadFile)

    ant.wtkpackage(
        jadFile: project.ext.jadFile,
        jarFile: project.ext.jarFile,
        autoversion: doAutoversion,
        obfuscate: doObfuscate,
        preverify: false) {
        bootclasspath {
            pathElement(path: configurations.bootClasspath.asPath)
        }
        classpath {
            pathElement(path: configurations.compile.asPath)
        }
        ant.fileSet(dir: sourceSets.main.output.classesDir.toString())
        ant.fileSet(dir: sourceSets.main.output.resourcesDir.toString())
    }
   
}

task compileJava(overwrite: true, dependsOn: wtkbuild) << {
}

project.tasks["assemble"].dependsOn {
    [
            wtkpackage
    ]
}

