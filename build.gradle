buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.eriwen:gradle-cobertura-plugin:1.0'
        classpath files(antennaDir)
    }
}

// antenna ant tasks
ant.taskdef(resource: 'antenna.properties') {
    classpath {
        pathElement(path: buildscript.configurations.classpath.asPath)
    }
}

allprojects {

}

subprojects {
    repositories {
        mavenCentral()
    }

    apply plugin: 'java'
    apply plugin: 'findbugs'
    apply plugin: 'jdepend'
    apply plugin: 'pmd'
    apply plugin: 'cobertura'


    configurations {
        bootClasspath
        testBootClasspath
    }

    dependencies {
        bootClasspath files(wtkHome + '/lib/midp_2.1.jar')
        bootClasspath files(wtkHome + '/lib/cldc_1.1.jar')

	testBootClasspath files(rtjar)
	testBootClasspath files(jssejar)
	testBootClasspath configurations.bootClasspath

	compile configurations.bootClasspath 
	// These are now valid for windows. Check Mac+Linux as well
        compile files(wtkHome + '/lib/cdc_1.1.jar')
        compile files(wtkHome + '/lib/jsr172_1.0.jar')
        compile files(wtkHome + '/lib/jsr_280_1.0.jar')

        testCompile "junit:junit:4.11"
        testCompile 'net.sourceforge.cobertura:cobertura:1.9.4.1'
        testCompile "org.mockito:mockito-all:1.9.5"
        testCompile "org.powermock:powermock-api-mockito:1.5"
        testCompile "org.powermock:powermock-module-junit4:1.5"
    }

    // Don't fail the build for static analysis warnings
    project.findbugs.ignoreFailures = true
    project.pmd.ignoreFailures = true

    /**
     * Don't scan for test classes, as there will be an error with JavaME compiled classes
     */
    tasks.withType(Test) {
        scanForTestClasses = false
        include "**/*Test.class" // whatever Ant pattern matches the test class files
    }

    project.ext.target = buildDir.toString()


    buildscript {
        dependencies {
            classpath files('../' + antennaDir)
        }
    }

    // antenna ant tasks
    ant.taskdef(resource: 'antenna.properties') {
        classpath {
            pathElement(path: buildscript.configurations.classpath.asPath)
        }
    }

    // file names
    project.ext.jarFile = target + '/' + jarFileName

    // wtk properties
    ant.property(name: 'wtk.home', value: wtkHome)
    ant.property(name: 'wtk.cldc.version', value: cldcVersion)
    ant.property(name: 'wtk.midp.version', value: midpVersion)

    
    task preverify << {
        project.ext.errors = false
        if (wtkHome == null || ''.equals(wtkHome)) {
            println('wtkHome was not set, edit the wtkHome property in gradle.properties')
            project.ext.errors = true
        } else if (!file(wtkHome).exists() || !file(wtkHome).isDirectory()) {
            println('wtkHome variable must point to the installatio of the Java ME SDK')
            project.ext.errors = true
        }
        
        if (antennaDir == null || ''.equals(antennaDir)) {
            println('antennaDir was not set, edit the antennaDir property in gradle.properties')
            project.ext.errors = true
        }

        if (rtjar == null || ''.equals(rtjar)) {
            println("Variable 'rtjar' is not set. You will need to point this variable on the JRE rt.jar")
            project.ext.errors = true
        } else if (!file(rtjar).exists() || !file(rtjar).isFile()) {       
            println("Variable 'rtjar' must point to the runtime jar in the JRE installation")
            project.ext.errors = true
        }
        
        if (jssejar == null || ''.equals(jssejar)) {
            println("Variable 'jssejar' is not set. You will need to point this variable on security extensions in the JRE installation")
            project.ext.errors = true
        } else if (!file(jssejar).exists() || !file(jssejar).isFile()) {
            println("Variable 'jssejar' must point to the security extensions ir in the JRE installation")
            project.ext.errors = true
        }
        
        if (project.ext.errors) {
            println("Error: Missing variables. Failing build")
            System.exit(1)
        }
        
    }

    task init(dependsOn: preverify) << {
        if (buildDir.isDirectory()) {
            ant.delete(dir: sourceSets.main.output.classesDir)
        }

        buildDir.mkdirs()
    }

    task wtkbuild(dependsOn: init) << {
        sourceSets.main.output.classesDir.mkdirs()
        sourceSets.main.output.resourcesDir.mkdirs()
       
        ant.wtkbuild(
            destdir: sourceSets.main.output.classesDir,
            encoding: 'UTF-8',
            source: javaVersion,
            srcDir: 'src', // TODO: Change this to a variable that's used to define the sourceSet
            includeantruntime: false,
            preverify: false) {
            bootclasspath {
                pathElement(path: configurations.bootClasspath.asPath)
            }
            classpath {
                pathElement(path: configurations.compile.asPath)
            }
        }
    }
}
